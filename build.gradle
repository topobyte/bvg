wrapper {
    gradleVersion = '5.2.1'
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'

    repositories {
        maven {
            url 'http://mvn.topobyte.de'
        }
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

ext.libraries = [
    project(':bvg'),
    project(':bvg-awt'),
    project(':bvg-android'),
    project(':bvg-util'),
    project(':bvg-inkscape4j')
]

configure(libraries) {

    apply plugin: 'maven'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    def runForArtifactPublication = project.hasProperty("topobyte")

    if (runForArtifactPublication) {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'
    }

    def global = new File(rootProject.projectDir, 'info.pom');
    def local = new File(project.projectDir, 'info.pom');

    def pomInfo = new XmlSlurper().parse(global);
    def projName = pomInfo.name
    def projDesc = pomInfo.description
    if (local.exists()) {
        def pomInfoL = new XmlSlurper().parse(local);
        projName = pomInfoL.name
        projDesc = pomInfoL.description
    }

    afterEvaluate { project ->
        uploadArchives {
            repositories {
                mavenDeployer {
                    pom.project {
                        name projName
                        description projDesc
                        url pomInfo.url
                        licenses {
                            license {
                                name pomInfo.licenseName
                                url pomInfo.licenseUrl
                                distribution pomInfo.licenseDistribution
                            }
                        }
                    }
                    pom.whenConfigured {
                        p -> p.dependencies = p.dependencies.findAll {
                            dep -> dep.scope != "test"
                        }
                    }
                }
            }
        }
    }
}

project(':bvg') {
    dependencies {
        compile 'net.jpountz.lz4:lz4:1.3.0'
        compile 'org.slf4j:slf4j-api:1.6.1'
    }
    eclipse {
        project {
            name = 'bvg-core'
        }
    }
}

project(':bvg-awt') {
    dependencies {
        compile project(':bvg')
    }
}

project(':bvg-android') {
    dependencies {
        compile project(':bvg')
        compile 'com.google.android:android:4.1.1.4'
    }
}

project(':bvg-inkscape4j') {
    dependencies {
        compile project(':bvg')
        compile 'de.topobyte:inkscape4j-core:0.0.7'
        compile 'de.topobyte:inkscape4j-writer:0.0.7'
    }
}

project(':bvg-util') {
    dependencies {
        compile project(':bvg')
        compile project(':bvg-awt')
        compile project(':bvg-inkscape4j')
        compile 'de.topobyte:commons-cli-helper:0.1.0'
        compile 'de.topobyte:melon-strings:0.0.1'
        compile 'org.apache.xmlgraphics:batik-bridge:1.12'
        compile 'org.apache.xmlgraphics:batik-transcoder:1.12'
        compile 'org.apache.xmlgraphics:batik-gvt:1.12'
        compile 'org.apache.xmlgraphics:batik-anim:1.12'

        testCompile 'de.topobyte:system-utils:0.0.1'
        testRuntime 'org.slf4j:slf4j-log4j12:1.6.1'
        testRuntime 'log4j:log4j:1.2.16'
    }
}

project(':bvg-cli') {
    dependencies {
        compile project(':bvg-util')

        runtime 'org.slf4j:slf4j-log4j12:1.6.1'
        runtime 'log4j:log4j:1.2.16'
    }
}
