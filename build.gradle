wrapper {
    gradleVersion = '8.12'
}

allprojects {
    group = pGroup
    version = pVersion
}

ext.libraries = [
    project(':bvg'),
    project(':bvg-awt'),
    project(':bvg-android'),
    project(':bvg-util'),
    project(':bvg-inkscape4j')
]

configure(libraries) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
}

configure(subprojects.findAll {it !in libraries}) {
    apply plugin: 'java'
}

subprojects {
    apply plugin: 'eclipse'

    repositories {
        maven {
            url = 'https://mvn.topobyte.de'
        }
        mavenCentral()
    }

    java {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

configure(libraries) {
    java {
        withSourcesJar()
    }

    def runForArtifactPublication = project.hasProperty("topobyte")

    if (runForArtifactPublication) {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'
    }

    def global = new File(rootProject.projectDir, 'info.pom');
    def local = new File(project.projectDir, 'info.pom');

    def pomInfo = new XmlSlurper().parse(global);
    def projName = pomInfo.name
    def projDesc = pomInfo.description
    if (local.exists()) {
        def pomInfoL = new XmlSlurper().parse(local);
        projName = pomInfoL.name
        projDesc = pomInfoL.description
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                pom {
                    name = "$projName"
                    description = "$projDesc"
                    url = "$pomInfo.url"
                    licenses {
                        license {
                            name = "$pomInfo.licenseName"
                            url = "$pomInfo.licenseUrl"
                            distribution = "$pomInfo.licenseDistribution"
                        }
                    }
                }
            }
        }   
    }
}

project(':bvg') {
    dependencies {
        api 'net.jpountz.lz4:lz4:1.3.0'
        api 'org.slf4j:slf4j-api:1.6.1'
    }
    eclipse {
        project {
            name = 'bvg-core'
        }
    }
}

project(':bvg-awt') {
    dependencies {
        api project(':bvg')
    }
}

project(':bvg-android') {
    dependencies {
        api project(':bvg')
        api 'com.google.android:android:4.1.1.4'
    }
}

project(':bvg-inkscape4j') {
    dependencies {
        api project(':bvg')
        api 'de.topobyte:inkscape4j-core:0.0.7'
        api 'de.topobyte:inkscape4j-writer:0.0.7'
    }
}

project(':bvg-util') {
    dependencies {
        api project(':bvg')
        api project(':bvg-awt')
        api project(':bvg-inkscape4j')
        api 'de.topobyte:commons-cli-helper:0.1.0'
        api 'de.topobyte:melon-strings:0.0.1'
        api 'org.apache.xmlgraphics:batik-bridge:1.12'
        api 'org.apache.xmlgraphics:batik-transcoder:1.12'
        api 'org.apache.xmlgraphics:batik-gvt:1.12'
        api 'org.apache.xmlgraphics:batik-anim:1.12'

        testImplementation 'de.topobyte:system-utils:0.0.1'
        testRuntimeOnly 'org.slf4j:slf4j-log4j12:1.6.1'
        testRuntimeOnly 'log4j:log4j:1.2.16'
    }
}

project(':bvg-cli') {
    dependencies {
        implementation project(':bvg-util')

        runtimeOnly 'org.slf4j:slf4j-log4j12:1.6.1'
        runtimeOnly 'log4j:log4j:1.2.16'
    }
}
